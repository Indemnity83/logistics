plugins {
	id 'net.fabricmc.fabric-loom' version '1.14.10'
	id 'maven-publish'
	id 'checkstyle'
	id 'com.diffplug.spotless' version '8.2.1'
}

// Allow CI to override version via MOD_VERSION env var
version = System.getenv("MOD_VERSION") ?: project.mod_version

group = project.maven_group

loom {
	splitEnvironmentSourceSets()

	mods {
		logistics {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

	runs {
		client {
			vmArg "-Dlogistics.timing=true"
		}
	}
}

repositories {
	mavenCentral()
	maven { url = "https://maven.fabricmc.net/" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	implementation "net.fabricmc:fabric-loader:${project.loader_version}"

	implementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 25
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_25
	targetCompatibility = JavaVersion.VERSION_25

	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archives_base_name}"}
	}
}

// Code formatting with Spotless
// Note: spotlessCheck is excluded from build to keep linting separate (run './gradlew spotlessCheck' explicitly)
spotless {
	enforceCheck false
	java {
		target 'src/*/java/**/*.java'
		importOrder('java|javax', 'com.logistics', '', '\\#')
		removeUnusedImports()
		palantirJavaFormat('2.85.0')
		formatAnnotations()
	}
	json {
		target 'src/**/*.json'
		prettier()
			.config(['printWidth': 120, 'tabWidth': 2])
	}
}

// Checkstyle configuration
checkstyle {
	toolVersion = '10.21.4'
	configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
	maxWarnings = 0
}

tasks.withType(Checkstyle).configureEach {
	reports {
		xml.required = false
		html.required = true
	}
}

check.dependsOn spotlessCheck
